CMAKE_MINIMUM_REQUIRED(VERSION 3.21)
PROJECT(DiagramSmurf)

IF(TARGET diagram_smurf_lib)
	return()
ENDIF()

SET(CMAKE_CXX_STANDARD 20)

OPTION(BRINGAUTO_TESTS "Enable tests" OFF)
OPTION(BRINGAUTO_SAMPLES "Enable build of sample applications" OFF)
OPTION(BRINGAUTO_INSTALL "Enable install feature" OFF)
OPTION(BRINGAUTO_PACKAGE "Enable package creation" OFF)
OPTION(BRINGAUTO_SYSTEM_DEP "Use system dependencies" OFF)

IF (NOT BRINGAUTO_SYSTEM_DEP)
	FIND_PACKAGE(CMLIB
			COMPONENTS CMDEF CMUTIL STORAGE
			REQUIRED
			)
	INCLUDE(cmake/Dependencies.cmake)
ENDIF ()

SET(CMAKE_POSITION_INDEPENDENT_CODE  ON)

FILE(GLOB_RECURSE source_files "source/*")
ADD_LIBRARY(diagram_smurf_lib STATIC ${source_files})
TARGET_INCLUDE_DIRECTORIES(diagram_smurf_lib PUBLIC "$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>")

IF (BRINGAUTO_TESTS)
	ENABLE_TESTING()
	ADD_SUBDIRECTORY("test")
ENDIF()

IF (BRINGAUTO_SAMPLES)
	ADD_EXECUTABLE(diagramSmurf example/main.cpp)
	TARGET_LINK_LIBRARIES(diagramSmurf PUBLIC diagram_smurf_lib)
ENDIF(BRINGAUTO_SAMPLES)

IF (BRINGAUTO_INSTALL)
	INCLUDE(GNUInstallDirs)

	TARGET_INCLUDE_DIRECTORIES(diagram_smurf_lib
			PUBLIC
			"$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}/state_smurf/>"
			)
	INSTALL(TARGETS diagram_smurf_lib
			EXPORT StateSmurfTargets
			LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
			ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
			RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
			INCLUDES DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
			)
	INSTALL(
			DIRECTORY
			${CMAKE_CURRENT_SOURCE_DIR}/include/
			DESTINATION
			${CMAKE_INSTALL_INCLUDEDIR}/state_smurf
			)
ENDIF()